---
layout: post
title:  "在屏幕顺序显示a-z（三）"
date:   2019-08-28 11:13
categories: 《汇编语言》
tags: Assembly 王爽
excerpt: 文中程序练习记录
author: Pythonbug
mathjax: true
---

## 新增功能
按下Esc之后，字符的颜色会改变。

## 分析
按下键盘的按键就是外部中断，执行int 9的中断程序。
当然，原来的int 9程序肯定是没有按下Esc键改变字符颜色的功能的。
所以，我们需要扩展一下原先int 9的功能
```
assume cs:code

stack segment
    db 128 dup(0)
stack ends

data segment
    dw 0,0
data ends

start:  mov ax,stack
        mov ss,ax
        mov sp,128          ;先搞栈

        mov ax,data
        mov ds,ax           ;再搞段

        mov ax,0
        mov es,ax

        push es:[9*4]
        pop ds:[0]
        push es:[9*4+2]
        pop ds:[2]          ;将原来int 9中断例程的入口地址保存在ds:0,ds:2单元

        mov word ptr es:[9*4],offset int9
        mov es:[9*4+2],cs

        mov ax,0b800h
        mov es,ax
        mov ah,'a'
    s:  mov es:[160*12+40*2],ah
        call delay
        inc ah
        cmp ah,'z'
        jna s

        mov ax,0
        mov es,ax

        push ds:[0]
        pop es:[9*4]
        push ds:[2]
        pop es:[9*4+2]          ;将中断向量表中int 9中断例程的入口恢复为原来的地址

        mov ax,4c00h
        int 21h

delay:  push ax
        push dx
        mov dx,1000h
        mov ax,0

    s1: sub ax,1
        sbb dx,0
        cmp ax,0
        jne s1
        cmp dx,0
        jne s1
        pop dx
        pop ax
        ret

;-----------------------以下为新的int 9中断例程---------------------------------
int9:   push ax
        push bx
        push es

        in al,60h

        pushf       ;flag入栈
        pushf       ;flag入栈
        pop bx
        and bh,11111100b ;IF,TF为0
        push bx
        popf
        call dword ptr ds:[0]       ;调用原来的int 9程序
        ;1、读出中断码
        ;2、字符的扫描码---》BIOS键盘缓存区
        ;2.1、控制键和切换键---》写入内存存储状态字节的单元
        ;
        cmp al,1
        jne int9ret

        mov ax,0b800h
        mov es,ax
        inc byte ptr es:[160*12+40*2+1]

int9ret:    pop es
            pop bx
            pop ax
            iret

end start
```

## 视频小结
<video id="video" controls="" preload="none" source id="mp4" src="https://my-blog-video.oss-cn-shanghai.aliyuncs.com/%E5%9C%A8%E5%B1%8F%E5%B9%95%E9%A1%BA%E5%BA%8F%E6%98%BE%E7%A4%BAa-z%EF%BC%88%E4%B8%89%EF%BC%89" width="800" height="480" type="video/mp4">
</video>
